// release docs: http://central.sonatype.org/pages/ossrh-guide.html

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

repositories {
    mavenCentral()
}

group = "com.github.matejtymes"
archivesBaseName = "javafixes"
// todo: add changelog
// version number meaning: majorRewrite.newFeature.minorRefactor.fixOrInternalRefactorOrInternalUpdate
//version = "1.4.2.0-SNAPSHOT"
version = "1.4.2.0"

dependencies {

    compile 'org.apache.commons:commons-lang3:3.14.0'
    compile 'org.slf4j:slf4j-api:1.7.36'

    testCompile 'junit:junit:4.13.2'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
//    testCompile 'org.apache.commons:commons-math3:3.2'
    testCompile 'ch.qos.logback:logback-classic:1.4.14'
    testCompile 'com.google.guava:guava:33.0.0-jre'
    testCompile 'org.mockito:mockito-all:1.10.19'
    testCompile 'commons-io:commons-io:2.15.1'

}


task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Java Fixes'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'Things I would like to have in Java by default'
                url 'https://github.com/MatejTymes/JavaFixes'

                scm {
                    connection 'scm:git:git://github.com/MatejTymes/JavaFixes.git'
                    developerConnection 'scm:git:ssh://github.com:MatejTymes/JavaFixes.git'
                    url 'https://github.com/MatejTymes/JavaFixes/tree/master'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'mtymes'
                        name 'Matej Tymes'
                        url 'https://github.com/MatejTymes'
                    }
                }
            }
        }
    }
}